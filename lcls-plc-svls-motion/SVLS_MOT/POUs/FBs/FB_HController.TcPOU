<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.11">
  <POU Name="FB_HController" Id="{aa256504-dbce-4b10-a9b8-ece4e52985f5}" SpecialFunc="None">
    <Declaration><![CDATA[
FUNCTION_BLOCK FB_HController IMPLEMENTS I_HController
VAR
    iMotionAxisDetAngle : I_MotionAxis;
    iCalculateHFromAngle : I_CalculateHFromAngle;
    stMotionStageAxisDetAngle : REFERENCE TO ST_MotionStage;
    stMotionStageAxisDetH : REFERENCE TO ST_MotionStage;

    fHPositionMeasured : LREAL;
    fHPositionSetpoint : LREAL;

    fHPositionGain : LREAL;
    fHPositionAdjustmentIntegration : LREAL;

    nEncoderCounts AT %Q* : ULINT; // nm/count

    rtMoveCmdTrigger : R_TRIG;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
fHPositionMeasured := iCalculateHFromAngle.CalculateH(
    Radians := DEG_TO_RAD(iMotionAxisDetAngle.MeasuredPosition)
);

nEncoderCounts := TO_ULINT(fHPositionMeasured * 1E6);

fHPositionSetpoint := iCalculateHFromAngle.CalculateH(
    Radians := DEG_TO_RAD(iMotionAxisDetAngle.SetpointPosition)
);

rtMoveCmdTrigger(CLK := stMotionStageAxisDetAngle.bExecute);

IF rtMoveCmdTrigger.Q THEN
    stMotionStageAxisDetH.bMoveCmd := TRUE;
    stMotionStageAxisDetH.fPosition := iCalculateHFromAngle.CalculateH(
        Radians := DEG_TO_RAD(stMotionStageAxisDetAngle.fPosition)
    );
END_IF

IF NOT stMotionStageAxisDetH.bBusy THEN
    stMotionStageAxisDetAngle.bExecute := FALSE;
END_IF
]]></ST>
    </Implementation>
    <Method Name="FB_Init" Id="{411c437b-e74e-4a25-8ab5-d2a195e88b34}">
      <Declaration><![CDATA[
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)

    iMotionAxisDetAngle : I_MotionAxis;
    iCalculateHFromAngle : I_CalculateHFromAngle;
    stMotionStageAxisDetAngle : REFERENCE TO ST_MotionStage;
    stMotionStageAxisDetH : REFERENCE TO ST_MotionStage;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.iMotionAxisDetAngle := iMotionAxisDetAngle;
THIS^.iCalculateHFromAngle := iCalculateHFromAngle;
THIS^.stMotionStageAxisDetAngle REF= stMotionStageAxisDetAngle;
THIS^.stMotionStageAxisDetH REF= stMotionStageAxisDetH;
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>